allprojects {
  group = 'com.plarpebu'
  version = '2.0.0-SNAPSHOT'

  apply plugin: 'java'

}

subprojects {

  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  
  repositories {
    jcenter()
  }

  compileJava {
    options.encoding = "CP1252"
  }

  dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
  }

}

task releasePlarpebu(
  type: Zip,
  description: "Builds a distributable zip file containing Plarpebu."
) {
  def zipFile = project.name.capitalize()
  into("$zipFile-${project.version}") {
    from file('includes')
    exclude 'libs'
  }
  into("$zipFile-${project.version}/libs") {
    from project(':Player').configurations.runtime
  }
  into("$zipFile-${project.version}") {
    from file("${project(':Player').buildDir}/libs")
    rename { "player.jar" }
  }
  into("$zipFile-${project.version}/plugins") {
    from fileTree(dir: '.').filter {
      it.getName().endsWith(".jar") &&
        (it.getName().contains('AlbumGrabberPlugin') ||
          it.getName().contains('BasicPlugins') ||
          it.getName().contains('CdgPlugin') ||
          it.getName().contains('ExamplePlugins'))
    }
  }
  archiveName = "$zipFile-${project.version}.zip"
}

releasePlarpebu.dependsOn(":AlbumGrabberPlugin:build")
releasePlarpebu.dependsOn(":CdgPlugin:build")
releasePlarpebu.dependsOn(":ExamplePlugins:build")
